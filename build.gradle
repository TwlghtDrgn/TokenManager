import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

group 'me.realized'
version '3.2.8'

compileJava.options.encoding = 'UTF-8'

processResources {
    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded"
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    from(sourceSets.main.resources.srcDirs) {
//        include '**/*.yml'
        filter(ReplaceTokens, tokens: [NAME: project.name, VERSION: project.version, SHADED_GROUP: group])
    }
}

repositories {
    mavenCentral()

    maven {
        name 'papermc-repo'
        url 'https://repo.papermc.io/repository/maven-public/'
    }

    maven {
        name 'bungeecord-repo'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }

    maven {
        name 'clip-repo'
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }

    maven {
        name 'codemc-repo'
        url 'https://repo.codemc.io/repository/maven-public/'
    }

    maven {
        name 'inventive-repo'
        url 'https://repo.inventivetalent.org/content/groups/public/'
    }

    maven {
        name 'jitpack'
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // shaded dependencies
    implementation 'redis.clients:jedis:3.5.1'
    implementation 'org.apache.commons:commons-pool2:2.4.2'
    implementation 'com.zaxxer:HikariCP:4.0.2'
    implementation 'org.slf4j:slf4j-nop:1.7.32'
    implementation 'org.bstats:bstats-bukkit:1.7'
    implementation 'com.github.plan-player-analytics:Plan:5.5.2307'
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
    archiveBaseName.set(rootProject.name)
    archiveVersion.set(String.valueOf(version))
    archiveClassifier.set("MC1.19+")

    dependencies {
        include(dependency('redis.clients:.*'))
        include(dependency('org.apache.commons:.*'))
        include(dependency('com.zaxxer:.*'))
        include(dependency('org.slf4j:.*'))
        include(dependency('org.bstats:.*'))
    }

    def group = project.group.toString() + "." + project.name.toLowerCase() + ".shaded."

    relocate 'redis.clients', group + 'redis.clients'
    relocate 'org.apache.commons.pool2', group + 'apache.commons.pool2'
    relocate 'com.zaxxer.hikari', group + 'zaxxer.hikari'
    relocate 'org.slf4j', group + "slf4j"
    relocate 'org.bstats', group + 'bstats'
}

build.dependsOn(shadowJar)